* emacs
** reopen killed buffer(file)
   #+BEGIN_SRC emacs-lisp
     ;; http://emacs.stackexchange.com/a/3334/10850
     (defvar killed-file-list nil
       "List of recently killed files.")

     (defun add-file-to-killed-file-list ()
       "If buffer is associated with a file name or directory,
      add that file to the `killed-file-list' when killing the buffer."
       (when buffer-file-name
         (push buffer-file-name killed-file-list))
       (when list-buffers-directory
         (push list-buffers-directory killed-file-list)))

     (add-hook 'kill-buffer-hook #'add-file-to-killed-file-list)

     (defun reopen-killed-file ()
       "Reopen the most recently killed file, if one exists."
       (interactive)
       (when killed-file-list
         (find-file (pop killed-file-list))))

     (defun reopen-killed-file-fancy ()
       "Pick a file to revisit from a list of files killed during this
     Emacs session."
       (interactive)
       (if killed-file-list
           (let ((file (completing-read "Reopen killed file: " killed-file-list
                                        nil nil nil nil (car killed-file-list))))
             (when file
               (setq killed-file-list (cl-delete file killed-file-list :test #'equal))
               (find-file file)))
         (error "No recently-killed files to reopen")))
   #+END_SRC
